varsearchstr = ""
varscount = 30
varindex = 0
varnum = -1
vartypes = 7
varnotypes = 0
varflags = 16
varnoflags = 128
varsmore = 0
varflagnames = "persist readonly rewrite world complete texture client server hex admin unknown args preload gamepreload"

newgui vars [
    guiheader "variables and commands"
    numvars = (getvarinfo -1 $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
    if (>= $varnum $numvars) [ varnum = -1 ]
    guilist [
        guistrut 1
        guifield varsearchstrval 32 [varsearchstr = $varsearchstrval; varnum = -1; varindex = 0] -1 0 "" 0 "^fd<enter search terms>" 1
        guispring 1
        guilist [
            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
            guistrut 0.15
            guilist [
                guitext " ^fashow " 
                guibitfield "commands  " vartypes (<< 1 3) [varnum = -1] 
                guicheckbox "advanced filters  " varsmore
            ]
            guistrut 0.15
        ]
        guispring 1
        guilist [
            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
            guistrut 0.15
            guilist [
                guistrut 1
                guitext (format "^fg%1 ^fa%2 found" $numvars (? (= $numvars 1) "match" "matches"))
                guistrut 1
            ]
            guistrut 0.15
        ]
    ]
    guistrut 0.5
    guilist [
        guilistx 2 [
            guifont "little" [
                guicontainer [1] [
                    guilist [
                        guilist [
                            //guistrut $varscount 1
                            varindex = (min (max 0 (- $numvars $varscount)) $varindex) //safeguard
                            varnum = (min $varnum $numvars)
                            guilist [
                                guistrut 39 1
                                break = 0
                                loopwhile i $varscount [= $break 0] [
                                    q = (+ $varindex $i)
                                    if (< $q $numvars) [
                                        curvar = (getvarinfo $q $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                                        hilvar = (stringreplace $curvar $varsearchstr (format "^fs^fy%1^fS" $varsearchstr))
                                        guilist [ guiradio $hilvar varnum $q ]
                                    ] [ guistrut 1 ]
                                ]
                            ]
                        ]
                    ]
                    guislider varindex 0 (max (- $numvars $varscount) 0) [] 1 1
                ] [
                    guistrut 43.25
                    //guistrut (+f $varscount 1) 1
                ]
            ]
        ]
        guistrut 1
        guilist [
            guistrut ($varsmore 39 59) 1
            if (&& (>= $varnum 0) (< $varnum $numvars) (> $numvars 0)) [
                scurvar = (getvarinfo $varnum $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                scurvartype = (getvartype $scurvar)
                guifont "emphasis" [ guitext $scurvar ]
                guilist [
                    case $scurvartype 0 [
                        guitext "^fainteger"
                        guispring 1
                	if (& 256 (getvarflags $scurvar)) [
                            guitext "^fahex colour"
                            guispring 1
                            guitext "^famin: 0x000000"
                            guispring 1
                            guitext "^famax: 0xFFFFFF"
                        ] [    
                            guitext (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                            guispring 1
                            guitext (format "^famin: ^fw%1" (getvarmin $scurvar))
                            guispring 1
                            guitext (format "^famax: ^fw%1" (getvarmax $scurvar))
                            guispring 1
                            guitext (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                        ]
                    ] 1 [
                        guitext "^fafloat"
                        guispring 1
                        guitext (format "^famin: ^fw%1" (getfvarmin $scurvar))
                        guispring 1
                        guitext (format "^famax: ^fw%1" (getfvarmax $scurvar))
                        guispring 1
                        guitext (format "^fadefault: ^fw%1" (getfvardef $scurvar 1))
                    ] 2 [
                        guitext "^fastring"
                    ] 3 [
                        guitext "^facommand"
                    ] 4 [
                        guitext "^faalias"
                    ] 5 [
                        guitext "^falocal"
                    ] () [
                        guitext "^fa???"
                    ]
                ]
                if (= $scurvartype 2) [
                    guistrut 0.25
                    guilist [
                        guitext "^fadefault"
                        guispring 1
                        guieditor [@[scurvar]_vardef] (? $varsmore -43 -58) 8 4 -1 0 "" (getsvardef $scurvar 1)
                    ]
                ]
                guistrut 0.25
                if (> $scurvartype 2) [
                    guistrut (? $varsmore 50 65) 1
                    guilist [
                        guitext "^faconsole"
                        guistrut 1
                        guilist [
                            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
                            guistrut  0.2
                            guistayopen [ guibutton (format "/%1 ...   " $scurvar) [saycommand (format "/%1 " $scurvar)] [] "textures/menu" ]
                            guistrut  0.2
                        ]
                        guistrut 1
                        guilist [
                            guibackground $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
                            guistrut  0.2
                            guistayopen [ guibutton (format "/echo (%1 ...   " $scurvar) [saycommand (format "/echo (%1 " $scurvar)] [] "textures/menu" ]
                            guistrut  0.2
                        ]
                    ]
                ] [    
                    guistrut (? $varsmore 50 65) 1
                    if (& 256 (getvarflags $scurvar)) [
                        guilist [
                            guitext (format "^fadefault: ^fw%1" (hexcolour (getvardef $scurvar 1)))
                            guispring 1
                            guilist [
                                guibackground (getvardef $scurvar) 1.0  0xffffff 1.0 1
                                guistrut 8 1
                                guistrut 1.25
                            ]
                        ]    
                        guilist [
                            guitext "value:"
                            guistrut 1
                            [@[scurvar]_varval] = (hexcolour $$scurvar)
                            guifield [@[scurvar]_varval] 10 [@scurvar $[@@[scurvar]_varval]] -1 0 ""
                            guispring 1
                            guilist [
                                guibackground $$scurvar 1.0  0xffffff 1.0 1
                                guistrut 8 1
                                guistrut 1.25
                            ]
                        ]
                    ] [ // not a hex/coulour var
                        guilist [
                            guitext "value:"
                            guistrut 1
                            [@[scurvar]_varval] = $$scurvar
                            guifield [@[scurvar]_varval] (? $varsmore -43 -58) [@scurvar $[@@[scurvar]_varval]] -1 0 ""
                        ]
                        // check if the max value is 2^n -1 --> could be a bitlist
                        m = (getvarmax $scurvar)
                        if (= 0 (& $m (+ $m 1))) [
                            guilist [
                                guitext "bitwise:"
                                guistrut 1
                                loopwhile i 24 [< (<< 1 $i) $m] [
                                    j = 
                                    guibitfield  "" $scurvar (<< 1 $i)
                                ]
                            ]
                            if (=s $guirollovertype "bitfield") [guitooltip "toggles a bit value: 1 2 4 8 16 ..."]
                        ]    
                    ]
                ]
                scurusage = (getvarusage $scurvar)
                if (stringlen $scurusage) [
                    guistrut 0.25
                    guilist [
                        guitext "^fausage:"
                        guistrut 1
                        guitext (concat $scurvar $scurusage) "" -1 -1 1000
                    ]
                ]
                scurdesc = (getvardesc $scurvar)
                if (stringlen $scurdesc) [
                    guistrut 0.25
                    guilist [
                        guitext "^fainfo:"
                        guistrut 1
                        guitext $scurdesc "" -1 -1 1000
                    ]
                ]
            ] [
                guistrut (? $varsmore 50 65) 1
                guifont "emphasis" [ guitext "no variable or command selected" ]
                guifont "little" [
                    guitext "^fause the list on the left to pick a variable" point
                    guitext "^fause the search field at the top to narrow results" point
                ]
            ]
        ]
        guistrut 2
        if ($varsmore) [
            guilist [
                guistrut 15 1 
                guistrut 0.5
                guitext "^fafilter by type"
                guibitfield "integer" vartypes (<< 1 0) [varnum = -1] 
                guibitfield "float"   vartypes (<< 1 1) [varnum = -1] 
                guibitfield "string"  vartypes (<< 1 2) [varnum = -1] 
                guibitfield "alias"   vartypes (<< 1 4) [varnum = -1] 
                guibitfield "local"   vartypes (<< 1 5) [varnum = -1] 
                guistrut 0.5
                guitext "^faflags / exclude"
                loop i (listlen $varflagnames) [
                    guilist [
                        guibitfield "" varflags (<< 1 $i) [varnum = -1] 
                        guibitfield (at $varflagnames $i) varnoflags (<< 1 $i) [varnum = -1] 
                    ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        varindex = 0
        varnum = -1
    ]
]

cursoundnames = [ "xv" "cv" "cp" "fl" "xr" "mr" "" "" "" "ss" "" "pos" "" "" "" "m" "" "c" "cn" ]
cursoundwidth = [ 4 4 4 4 4 4 0 0 0 3 0 16 0 0 0 2 0 2 3 ]
newgui cursounds [
    cursoundnum = (getcursound)
    guilist [
        guilist [ guistrut 3 1 ]
        cursoundprop = (listlen $cursoundnames)
        loop j $cursoundprop [
            cursoundw = (at $cursoundwidth $j)
            if $cursoundw [
                cursoundval = (at $cursoundnames $j)
                guilist [
                    guistrut $cursoundw 1
                    guicenter [ guitext (format "^fy%1" $cursoundval) ]
                ]
            ]
        ]
        guilist [ guistrut 30 1 ]
    ]
    loop i $cursoundnum [
        guilist [
            guilist [
                guistrut 3 1
                guilist [ guitext (format "^fg%1." $i) ]
            ]
            cursoundplay = (getcursound $i 14)
            if $cursoundplay [ 
                cursoundprop = (getcursound $i)
                loop j $cursoundprop [
                    cursoundw = (at $cursoundwidth $j)
                    if $cursoundw [
                        cursoundval = (getcursound $i $j)
                        guilist [
                            guistrut $cursoundw 1
                            guicenter [ guitext $cursoundval ]
                        ]
                    ]
                ]
                guilist [
                    guistrut 30 1
                    guilist [
                        guispring
                        guitext (getsound (getcursound $i 15) (getcursound $i 9) 3)
                    ]
                ]
            ] [
                guispring
                guitext "-"
            ]
        ]
    ]
]
