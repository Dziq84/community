// Beha's HWBan System.
// Released under CC0; https://creativecommons.org/publicdomain/zero/1.0

// IDs of all registered bans.
hwban.bans = []

// Message upon ban.
hwban.message = "Ban evasion. If this ban is in error, please contact the Red Eclipse team. Do not continue to evade bans."

// Register a ban.
// <name> <checks>
hwban.ban = [
	local name checks

	name = $arg1
	checks = $arg2

	hwban.bans.$name.name = $name
	hwban.bans.$name.checks = $checks

	hwban.bans = (concat $hwban.bans $name)
]

// Start processing loop.
hwban.go = [
	// All clients.
	looplist cn (listclients 0 0) [
		// All bans.
		looplist name $hwban.bans [
			local match
			match = 1
			// Keep match as 1 if all checks pass.
			looplist check (getalias hwban.bans.$name.checks) [
				match = (&& $match (do $check))
			]
			// If matched and not AI, ban.
			if (&& $match (! (isai $cn)) (>= (getclientpriv -1) $banlock)) [
				echo "HWBan found match:" (getclientname $cn 1)
				ban $cn $hwban.message
			]
		]
	]

	// Run again later.
	sleep 2000 [hwban.go]
]
setcomplete hwban.go 1

// Start automatically.
hwban.go
