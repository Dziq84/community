//Advanced Map Manager by Beha
//Under CC0 1.0 License <https://creativecommons.org/publicdomain/zero/1.0/>

//Reset the map lists and preserve servermaster
amap._versioncfgpreserve = [
    servermaster
    sv_botmalenames sv_botfemalenames
    sv_botmalevanities sv_botfemalevanities
]
looplist i $amap._versioncfgpreserve [
    local name
    name = (format "amap._versioncfgpreserve_%1" $i)
    alias $name $$i
]
exec config/version.cfg
looplist i $amap._versioncfgpreserve [
    local name
    name = (format "amap._versioncfgpreserve_%1" $i)
    $i $$name
]

//Variables checked
//amap.<x>
//Other special variables are: disallow, general
amap._vars = "main capture defend king bomber hold race duel multi gladiator small medium large"

//Modes covered under amap.general
amap._generalmodes = "main capture defend bomber"

amap.small = (concat $amap.duel $amap.small)
looplist i $amap._generalmodes [
    local name
    name = (format "amap.%1" $i)
    $name = (concat $$name $amap.general)
]

//Create allowmaps from all other variables.
amap.allow = ""
looplist i $amap._vars [
    amap.allow = (concat $amap.allow $(format "amap.%1" $i))
]

//Loop through amap.<x>, assigning to the appropriate sv_ variable while ignoring duplicates.
amap._doset = [
    local name
    local svname
    name = (format "amap.%1" $arg1)
    svname = (format "sv_%1maps" $arg1)
    looplist i (getalias $name) [
        val = $i
        looplist j $$svname [
            if (=s $i $j) [
                val = ""
            ]
        ]
        if (!=s $val "") [
            $svname (concat $$svname $val)
        ]
    ]
]

//Assign amap variables to server, remove any disallowed maps.
looplist i (concat "allow" $amap._vars) [
    local svname
    local tmp
    amap._doset $i
    svname = (format "sv_%1maps" $i)
    tmp = ""
    looplist j $$svname [
        local good
        good = 1
        looplist k $amap.disallow [
            if (=s $j $k) [
                good = 0
            ]
        ]
        if (= $good 1) [
            tmp = (concat $tmp $j)
        ]
    ]
    $svname $tmp
]
