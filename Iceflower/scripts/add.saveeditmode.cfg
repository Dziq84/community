//Author: Iceflower
//Save editmode, only the selected priv level can edit but everyone can spawn

privlevtostring = [//arg1 = privlevel
	result (sublist "None Player Supporter Moderator Operator Administrator Developer Founder" $arg1 1)
]

setdesc "saveedit" "Save editmode, only the selected priv level can edit but everyone can spawn;^n0 = off, 1 = player, 2 = supporter, 3 = moderator, 4 = operator, 5 = administrator, 6 = developer, 7 = founder" "<value>"
saveedit = [saveeditmode $arg1] //because the content would spam the screen
saveeditmode = [
	editlev_v = $arg1
	if (& (getclientpriv ($getclientnum) $editlev_v) (= (gamemode) 1)) [ //execute the saveeditmode only if you have the required priv level and the gamemode is edit
		if (> $editlev_v 0) [
			mastermode 2
			spawneditlock 0 //everybody can spawn
			editlock $editlev_v //sets the choosen editlock
			looplist i (listdel (listclients 0) (listclients 0 2)) [	//non ai cn list; spectate all player which havent the required level
					if (getclientpriv $i $editlev_v) [] [spectator 1 $i]
			]
			me (format "^f[0xffde00]sets required level for editing to %1" (privlevtostring $editlev_v)) //feedback
		] [
			mastermode (getvardef "mastermode" 1)
			spawneditlock (getvardef "spawneditlock" 1)
			editlock (getvardef "editlock" 1)
			me "^f[0xffde00]turns save editmode off" //feedback
		]
	] [ // if saveeditmode isnt possible
		if (!= (gamemode) 1) [ //echo if you arent in edit
			echo "^f[0xFF3214]Acces denied, you need to be in ^f[0x359AF2]edit^f[0xFF3214]."
		] [ //when in edit then it echos only the required priv level
			echo (format "^f[0xFF3214]Acces denied, you have to be a ^f[0x359AF2]%1^f[0xFF3214]." (privlevtostring $editlev_v))
		]
	]
]